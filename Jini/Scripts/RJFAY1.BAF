
// Fay is cautious

IF
  G("RJFollowFY",0)
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),40)
THEN
  RESPONSE #100
    ClearActions("RJFay")
    RunAwayFromNoInterruptNoLeaveArea(NearestEnemyOf(Myself),20)
END

IF
  G("RJFollowFY",0)
  AttackedBy([ANYONE],DEFAULT)
THEN
  RESPONSE #100
    ClearActions("RJFay")
    RunAwayFromNoInterruptNoLeaveArea(LastAttackerOf(Myself),20)
END

// Fay will follow

IF
  G("RJFollowFY",1)
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),40)
THEN
  RESPONSE #100
    ClearActions("RJFay")
    MoveToObjectNoInterrupt(Player1)
END

IF
  G("RJFollowFY",1)
  AttackedBy([ANYONE],DEFAULT)
THEN
  RESPONSE #100
    ClearActions("RJFay")
    MoveToObjectNoInterrupt(Player1)
END

// Fay's talks trigger on chapter change

IF
  G("RJFayTalk",0)
  GGT("RJFaySpawn",1)
  !AreaCheck("RJB604")
  !AreaCheck("RJB605")
  !AreaCheck("RJB606")
  GLT("Chapter",13)
  GlobalsGT("Chapter","RJFayChap")
THEN
  RESPONSE #100
    RealSetGlobalTimer("RJFayRomance","GLOBAL",RJ_QUICK)
    IncrementGlobal("RJFayChap","GLOBAL",1)
    SetGlobal("RJFayTalk","GLOBAL",1)
END

// Fay's talk starts after a decorous pause

IF
  G("RJiniTalk",0)
  G("RJFayTalk",1)
  RealGlobalTimerExpired("RJFayRomance","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJFayTalks","GLOBAL",0)
    AddGlobals("RJFayTalks","RJFayChap")
    AddGlobals("RJFayTalks","RJFayChap")
    StartDialogueNoSet(Player1)
END

// Setup Sirene BG1 Romance Reports

IF
  G("RJSoD",0)
  InParty("C0Sirene")
  G("C0SireneMatch",1)
  !G("RJSIROMBG1",1)
  GGT("C0SireneTalksBG1",10)
  GLT("C0SireneTalksBG1",17)
THEN
  RESPONSE #100
    SG("RJSIROMBG1",1)
END

IF
  G("RJSoD",0)
  InParty("C0Sirene")
  G("C0SireneMatch",1)
  !G("RJSIROMBG1",2)
  GGT("C0SireneTalksBG1",16)
THEN
  RESPONSE #100
    SG("RJSIROMBG1",2)
END

IF
  G("RJSoD",0)
  InParty("C0Sirene")
  G("C0SireneMatch",2)
  !G("RJSIROMBG1",3)
THEN
  RESPONSE #100
    SG("RJSIROMBG1",3)
END

// Fay's Extra Talks

IF
  OR(2)
    G("RJFayExtra1",1)
    G("RJFayExtra2",1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
  !G("RJFayExtra3",0)
  AreaType(OUTDOOR)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

// Extra 1 - 1st return to sleep venue

IF
  G("RJFayExtra1",0)
  AreaCheck("RJB606")
  G("RJFayBig",1)
THEN
  RESPONSE #100
    SetGlobal("RJFayExtra1","GLOBAL",1)
END

// Extra 2 - Return to Candlekeep

IF
  G("RJFayExtra2",0)
  GGT("RJFaySpawn",1)
  AreaCheck("%rj_area%2626")
THEN
  RESPONSE #100
    SetGlobal("RJFayExtra2","GLOBAL",1)
END

// Extra 3 - Pokes

IF
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Branwen")
  !GlobalsEqual("RJRomBR","P#BranwenRomanceActive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomBR")
    SG("RJRomBR",0)
    AddGlobals("RJRomBR","P#BranwenRomanceActive")
    AddGlobals("RJRomTyp","RJRomBR")
    AddGlobals("RJPokeBR","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  G("RJSoD",1)
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("C0Sirene")
  !GlobalsEqual("RJRomSI","C02SireneRomanceActive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomSI")
    SG("RJRomSI",0)
    AddGlobals("RJRomSI","C02SireneRomanceActive")
    AddGlobals("RJRomTyp","RJRomSI")
    AddGlobals("RJPokeSI","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  G("RJSoD",0)
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("C0Sirene")
  !GlobalsEqual("RJRomSI1","RJSIROMBG1")
THEN
  RESPONSE #100
    SG("RJRomSI1",0)
    AddGlobals("RJRomSI1","RJSIROMBG1")
    AddGlobals("RJPokeSI1","RJRomSI1")
    AddGlobals("RJFayExtra3","RJRomSI1")
END

IF
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Dynaheir")
  !GlobalsEqual("RJRomDY","X#DynaheirRomanceActive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomDY")
    SG("RJRomDY",0)
    AddGlobals("RJRomDY","X#DynaheirRomanceActive")
    AddGlobals("RJRomTyp","RJRomDY")
    AddGlobals("RJPokeDY","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Sharteel")
  !GlobalsEqual("RJRomSH","X#SharInterest")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomSH")
    SG("RJRomSH",0)
    AddGlobals("RJRomSH","X#SharInterest")
    AddGlobals("RJRomTyp","RJRomSH")
    AddGlobals("RJPokeSH","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Corwin")
  !GlobalsEqual("RJRomCO","bd_corwin_romanceactive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomCO")
    SG("RJRomCO",0)
    AddGlobals("RJRomCO","bd_corwin_romanceactive")
    AddGlobals("RJRomTyp","RJRomCO")
    AddGlobals("RJPokeCO","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  G("RJSoD",0)
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Neera")
  !GlobalsEqual("RJRomNE1","NEERA_ROMANCE")
THEN
  RESPONSE #100
    SG("RJRomNE1",0)
    AddGlobals("RJRomNE1","NEERA_ROMANCE")
    AddGlobals("RJPokeNE1","RJRomNE1")
    AddGlobals("RJFayExtra3","RJRomNE1")
END

IF
  G("RJSoD",1)
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Neera")
  !GlobalsEqual("RJRomNE","bd_neera_romanceactive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomNE")
    SG("RJRomNE",0)
    AddGlobals("RJRomNE","bd_neera_romanceactive")
    AddGlobals("RJRomTyp","RJRomNE")
    AddGlobals("RJPokeNE","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Safana")
  !GlobalsEqual("RJRomSA","bd_safana_romanceactive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomSA")
    SG("RJRomSA",0)
    AddGlobals("RJRomSA","bd_safana_romanceactive")
    AddGlobals("RJRomTyp","RJRomSA")
    AddGlobals("RJPokeSA","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("Viconia")
  !GlobalsEqual("RJRomVI","bd_viconia_romanceactive")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomVI")
    SG("RJRomVI",0)
    AddGlobals("RJRomVI","bd_viconia_romanceactive")
    AddGlobals("RJRomTyp","RJRomVI")
    AddGlobals("RJPokeVI","RJRomTyp")
    AddGlobals("RJFayExtra3","RJRomTyp")
END

IF
  G("RJSoD",0)
  GGT("RJFaySpawn",1)
  G("RJFayExtra3",0)
  See("rh#isra")
  !GlobalsEqual("RJRomIS","rh#IsraRomanceCheck")
THEN
  RESPONSE #100
    AddGlobals("RJRomPrv","RJRomIS")         
    SG("RJRomIS",0)
    AddGlobals("RJRomIS","rh#IsraRomanceCheck")
    AddGlobals("RJPokeIS","RJRomIS")
    AddGlobals("RJRomTyp","RJRomIS")
    AddGlobals("RJRomTyp","RJRomIS")
    IncrementGlobal("RJRomTyp","GLOBAL",-1)
    AddGlobals("RJFayExtra3","RJRomTyp")
END

// Fay's Banters

IF
  GGT("RJFaySpawn",1)
  G("RJFayBanterStart",0)
THEN
  RESPONSE #100
    SetGlobal("RJFayBanterStart","GLOBAL",1)
    RealSetGlobalTimer("RJFayBanterTimer","GLOBAL",RJ_BANTER)
END

IF
  !G("RJFayBanter",0)
  RealGlobalTimerExpired("RJFayBanterTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("RJFayBanter","GLOBAL",0)
END

IF
  G("RJFayBanter",0)
  G("RJSoD",0)
  CombatCounter(0)
  !See([ENEMY])
  RealGlobalTimerExpired("RJFayBanterTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("RJFayBanter","GLOBAL",1)
    RealSetGlobalTimer("RJFayBanterTimer","GLOBAL",RJ_BANTER)
    StartDialogueNoSet(Myself)
END

IF
  G("RJFayBanter",0)
  G("RJSoD",1)
  CombatCounter(0)
  !See([ENEMY])
  RealGlobalTimerExpired("RJFayBanterTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("RJFayBanter","GLOBAL",2)
    RealSetGlobalTimer("RJFayBanterTimer","GLOBAL",RJ_BANTER)
    StartDialogueNoSet(Myself)
END

// Fay initiated flirts

// Flirts initiate

IF
  !G("RJFayRomanceActive",0)
  G("RJFayFlirtsStart",0)
THEN
  RESPONSE #100
    SetGlobal("RJFayFlirtsStart","GLOBAL",1)
    RealSetGlobalTimer("RJFayFlirtTimer","GLOBAL",RJ_FAST)
END

// Ready for flirting

IF
  G("RJiniTalk",0)
  !AreaCheck("RJB604")
  !AreaCheck("RJB605")
  !AreaCheck("RJB606")
  RealGlobalTimerExpired("RJFayFlirtTimer","GLOBAL")
  !StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  See(Player1)
  CombatCounter(0)
  !See([ENEMY])
  G("RJFayFlirt",0)
THEN
  RESPONSE #100
    SetGlobal("RJFayFlirt","GLOBAL",1)
    StartDialogueNoSet(Player1)
END

// Flirting begins

IF
  G("RJFayFlirt",1)
  RealGlobalTimerExpired("RJFayFlirtTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("RJFayFlirt","GLOBAL",0)
    RealSetGlobalTimer("RJFayFlirtTimer","GLOBAL",RJ_FLIRT)
END

IF
  RealGlobalTimerExpired("RJFayFlirtTimer","GLOBAL")
  !CombatCounter(0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("RJFayFlirtTimer","GLOBAL",150)
END
