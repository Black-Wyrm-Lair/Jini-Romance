
// This is CHARNAME's main script. Each block does one unique action.

// Reselecting InParty NPCs

IF
  G("RJiniTalk",0)
  G("RJSelect",1)
THEN
  RESPONSE #100
    SetGlobal("RJSelect","GLOBAL",0)
    SetGlobal("RJIFA","GLOBAL",0)
    ActionOverride(Player2,MakeUnselectable(0))
    ActionOverride(Player3,MakeUnselectable(0))
    ActionOverride(Player4,MakeUnselectable(0))
    ActionOverride(Player5,MakeUnselectable(0))
    ActionOverride(Player6,MakeUnselectable(0))
END

// Rubbing the lamp

// Trying to Give Jini the 1st Rub

IF
  G("RJRUB",1)
  G("Chapter",0)
THEN
  RESPONSE #100
    SetGlobal("RJRUB","GLOBAL",0)
    SetGlobal("RJLamp","GLOBAL",1)
    StartDialogue("RJPLAYER",Player1)
END

// Already inside the lamp

IF
  G("RJRUB",1)
  OR(3)
    AreaCheck("RJB604")
    AreaCheck("RJB605")
    AreaCheck("RJB606")
THEN
  RESPONSE #100
    DisplayStringHead(Player1,@400)
    SetGlobal("RJRUB","GLOBAL",0)
    Wait(3)
END

// Already in progress

IF
  G("RJRUB",1)
  G("RJRUBX",0)
  OR(4)
    !G("RJLamp",0)
    !G("RJiniFlirt",0)
    !G("RJiniTalk",0)
    StateCheck(Player1,CD_STATE_NOTVALID)
THEN
  RESPONSE #100
    DisplayStringHead(Player1,@401)
    SetGlobal("RJRUB","GLOBAL",0)
    SetGlobal("RJRUBX","GLOBAL",1)
    Wait(3)
END

// Fix rub conflict

IF
  G("RJRUB",1)
  G("RJRUBX",1)
  !G("RJiniTalk",0)
THEN
  RESPONSE #100
    SetGlobal("RJLamp","GLOBAL",0)
    SetGlobal("RJiniTalk","GLOBAL",0)
    IncrementGlobal("RJiniTalks","GLOBAL",-1)
    SetGlobal("RJRUBX","GLOBAL",0)
END

// Get Jini outside the lamp

IF
  G("RJRUB",1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  !G("RJiniRomanceActive",2)
  !StateCheck(Player1,CD_STATE_NOTVALID)
THEN
  RESPONSE #80
    SetGlobal("RJRUB","GLOBAL",2)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

IF
  G("RJRUB",1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  G("RJiniRomanceActive",2)
  !StateCheck(Player1,CD_STATE_NOTVALID)
THEN
  RESPONSE #80
    SetGlobal("RJRUB","GLOBAL",2)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
  RESPONSE #20
    SetGlobal("RJRUB","GLOBAL",2)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJLamp","GLOBAL",9)
    SmallWait(20)
    StartDialogue("RJPLAYER",Player1)
END

// Initiate Self Talks

IF
  OR(9)
    G("Rjali1Met",1)
    G("RJShadow",1)
    G("RJIMTK1",1)
    G("RJIMTK2",1)
    G("RJIMTK3",1)
    G("RJIMTK4",1)
    G("RJJIRVL",1)
    G("RJINV204",1)
    G("RJINV705",1)
THEN
  RESPONSE #100
    StartDialogue("RJPLAYER",Player1)
END

IF
  AreaCheck("RJP13G")
  OR(4)
    G("RJiniRescue",3)
    G("RJiniRescue",6)
    G("RJiniRescue",9)
    G("RJiniRescue",12)
THEN
  RESPONSE #100
    StartDialogue("RJPLAYER",Player1)
END

//  Another Party Member has the Lamp

IF 
OR(5)
  HasItem("RJ1LAMP",player2)
  HasItem("RJ1LAMP",player3)
  HasItem("RJ1LAMP",player4)
  HasItem("RJ1LAMP",player5)
  HasItem("RJ1LAMP",player6)
THEN
  RESPONSE #100
    TakePartyItem("RJ1LAMP")
    DisplayStringHead(Player1,@15)
    Wait(3)
END

// Rescue from gender change

IF
  HasItem("RJ1LAMP",player1)
  !Gender(Player1,MALE)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  !AreaCheck("RJB604")
  !AreaCheck("RJB605")
  !AreaCheck("RJB606")
  G("RJGender",0)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJGender","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// This is where Jini's love talks start.

IF
  HasItem("RJ1LAMP",player1)
  !G("Chapter",0)
  G("RJiniTalks",0)
THEN
  RESPONSE #100
    SetGlobal("RJiniTalks","GLOBAL",1)
    RealSetGlobalTimer("RJiniRomance","GLOBAL",RJ_TALK)
END

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaType(OUTDOOR)
  OR(8)
    G("RJiniTalks",1)
    G("RJiniTalks",3)
    G("RJiniTalks",5)
    G("RJiniTalks",9)
    G("RJiniTalks",11)
    G("RJiniTalks",13)
    G("RJiniTalks",15)
    G("RJiniTalks",19)
  RealGlobalTimerExpired("RJiniRomance","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    IncrementGlobal("RJiniTalks","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// Post Rescue Lovetalk Resumption

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaType(OUTDOOR)
  G("RJiniRescue",99)
  G("RJiniTalks",7)
  RealGlobalTimerExpired("RJiniRomance","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    IncrementGlobal("RJiniTalks","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// Drop In Talks

IF
  G("RJiniTalk",1)
  OR(4)
    G("RJiniTalks",18)
    G("RJiniTalks",22)
    G("RJiniTalks",24)
    G("RJiniTalks",26)
THEN
  RESPONSE #100
    StartCutSceneMode()
    DisplayStringHead(Player1,@16)
    Wait(3)
    StartCutScene("RJDRP604")
END

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaType(OUTDOOR)
  OR(3)
    G("RJiniTalks",17)
    G("RJiniTalks",21)
    G("RJiniTalks",25)
  RealGlobalTimerExpired("RJiniRomance","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniTalk","GLOBAL",1)
    IncrementGlobal("RJiniTalks","GLOBAL",1)
END

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaType(OUTDOOR)
  G("RJJIRVL",99)
  G("RJiniTalks",23)
  RealGlobalTimerExpired("RJiniRomance","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniTalk","GLOBAL",1)
    IncrementGlobal("RJiniTalks","GLOBAL",1)
END

// Extra Talks

// 1 - Jini Investigates

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaType(OUTDOOR)
  G("RJiniExtra1",0)
  RealGlobalTimerExpired("RJiniExtraT1","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniExtra1","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// 2 - Cloakwood Mine

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaType(OUTDOOR)
  GGT("FLOODED",1)
  G("RJiniExtra2",0)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniExtra2","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// 3 - My Mysterious Shadow

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  G("RJiniExtra3",0)
  AreaType(OUTDOOR)
  GGT("RJShadowCnt",3)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniExtra3","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// 4 - Jini has checked things out in Candlekeep

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaCheck("%rj_area%2610")
  G("RJiniExtra4",0)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniExtra4","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// 5 - Jini urges you to take care on the final run

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  AreaCheck("%rj_area%0146")
  G("RJiniExtra5",0)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniExtra5","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END

// Imoen Talks

// Imoen: Equipment

IF
  InParty("%rj_imoen%")
  See("%rj_imoen%")
  G("RJIMTK1",0)
  HasItem("RJ1SW1H","%rj_imoen%")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck("%rj_imoen%",CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJIMTK1","GLOBAL",1)
END

// Imoen: Timestop

IF
  InParty("%rj_imoen%")
  See("%rj_imoen%")
  G("RJIMTK2",0)
  GGT("RJiniTalks",4)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck("%rj_imoen%",CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJIMTK2","GLOBAL",1)
END

// Imoen: Unvirgin

IF
  InParty("%rj_imoen%")
  See("%rj_imoen%")
  G("RJIMTK3",0)
  G("RJiniRomanceActive",2)
  RealGlobalTimerExpired("RJIMTIME1","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck("%rj_imoen%",CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJIMTK3","GLOBAL",1)
END

// Imoen: Meets Fay

IF
  InParty("%rj_imoen%")
  See("%rj_imoen%")
  G("RJIMTK4",0)
  GGT("RJFaySpawn",1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck("%rj_imoen%",CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJIMTK4","GLOBAL",1)
END

// Revealing Jini to your Party

IF
  G("RJJIRVL",0)
  G("RJiniPermit",1)
  GGT("Chapter",6)
  AreaType(OUTDOOR)
  NumInPartyAliveGT(1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJJIRVL","GLOBAL",1)
    RealSetGlobalTimer("RJiniRomance","GLOBAL",RJ_TALK)
END

IF
  G("RJJIRVL",0)
  G("RJiniPermit",1)
  GGT("Chapter",6)
  AreaType(OUTDOOR)
  NumInPartyAlive(1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJJIRVL","GLOBAL",99)
    RealSetGlobalTimer("RJiniRomance","GLOBAL",RJ_TALK)
END

// Rjali spotted in Friendly Arms

IF
  G("RJShadow",0)
  !G("RJShadowCnt",0)
  AreaCheck("%rj_area%2300")
  Global("RJALI","%rj_area%2300",0)
  Range("Door2305",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%2300",1)
    StartCutScene("RJSHADOW")
END

IF
  G("RJShadow",0)
  G("RJShadowCnt",0)
  AreaCheck("%rj_area%2300")
  Global("RJALI","%rj_area%2300",0)
  Range("Door2305",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%2300",1)
    StartCutScene("RJSHAD0W")
END

// Rjali spotted in Beregost

IF
  G("RJShadow",0)
  !G("RJShadowCnt",0)
  AreaCheck("%rj_area%3300")
  Global("RJALI","%rj_area%3300",0)
  Range("Door3351",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%3300",1)
    StartCutScene("RJSHADOW")
END

IF
  G("RJShadow",0)
  G("RJShadowCnt",0)
  AreaCheck("%rj_area%3300")
  Global("RJALI","%rj_area%3300",0)
  Range("Door3351",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%3300",1)
    StartCutScene("RJSHAD0W")
END

// Rjali spotted in Nashkel

IF
  G("RJShadow",0)
  !G("RJShadowCnt",0)
  AreaCheck("%rj_area%4800")
  G("Chapter",2)
  Global("RJALI","%rj_area%4800",0)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%4800",1)
    StartCutScene("RJSHADOW")
END

IF
  G("RJShadow",0)
  G("RJShadowCnt",0)
  AreaCheck("%rj_area%4800")
  G("Chapter",2)
  Global("RJALI","%rj_area%4800",0)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%4800",1)
    StartCutScene("RJSHAD0W")
END

// Rjali spotted in Ulgoth's Beard

IF
  G("RJShadow",0)
  AreaCheck("%rj_area%1000")
  Global("RJALI","%rj_area%1000",0)
  Range("Door1007",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%1000",1)
    StartCutScene("RJSHADOW")
END

// Rjali spotted in Baldur's Gate - Near Entrance

IF
  G("RJShadow",0)
  AreaCheck("%rj_area%0800")
  Global("RJALI","%rj_area%0800",0)
  Range("Door0148",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%0800",1)
    StartCutScene("RJSHADOW")
END

// Rjali spotted in Baldur's Gate - Near Palace

IF
  G("RJShadow",0)
  AreaCheck("%rj_area%0200")
  Global("RJALI","%rj_area%0200",0)
  Range("Door0004",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%0200",1)
    StartCutScene("RJSHADOW")
END

// Rjali spotted in Baldur's Gate - Near Circus

IF
  G("RJShadow",0)
  AreaCheck("%rj_area%0700")
  Global("RJALI","%rj_area%0700",0)
  Range("Door0720",30)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    StartCutSceneMode()
    SetGlobal("RJALI","%rj_area%0700",1)
    StartCutScene("RJSHADOW")
END

// Jini is romanceable, and she is going to initiate flirts!

IF
  HasItem("RJ1LAMP",player1)
  G("RJiniFlirtStart",0)
  G("RJiniRomanceActive",2)
THEN
  RESPONSE #100
    SetGlobal("RJiniFlirtStart","GLOBAL",1)
    RealSetGlobalTimer("RJiniFlirtTimer","GLOBAL",RJ_FLIRT)
END

IF
  HasItem("RJ1LAMP",player1)
  G("RJLamp",0)
  G("RJiniFlirt",0)
  G("RJiniTalk",0)
  !AreaCheck("RJB604")
  !AreaCheck("RJB605")
  !AreaCheck("RJB606")
  RealGlobalTimerExpired("RJiniFlirtTimer","GLOBAL")
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("RJiniFlirt","GLOBAL",1)
    SetGlobal("RJiniTalk","GLOBAL",1)
    SetGlobal("RJSelect","GLOBAL",1)
    CreateCreature("RJinii1",[-1.-1],0)
    ActionOverride(Player2,MakeUnselectable(45000))
    ActionOverride(Player3,MakeUnselectable(45000))
    ActionOverride(Player4,MakeUnselectable(45000))
    ActionOverride(Player5,MakeUnselectable(45000))
    ActionOverride(Player6,MakeUnselectable(45000))
END
